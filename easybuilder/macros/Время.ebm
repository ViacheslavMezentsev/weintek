// Ссылки:
// 1. Программируем временные сложности: http://www.codesys.ru/docs/TimeExperience.pdf


sub GetLocalTime( unsigned int time )

    bool IsLeapYear = false
    char DayOfWeek = 0, Seconds = 0, Minutes = 0, Hours = 0
    unsigned short Year = 0, Month = 0, Day = 0
    unsigned int uiDate = 0, uiTime = 0, uiDay = 0, days_to_month = 0, days_to_year = 0, y = 0
    
    unsigned short DoM[12] = { 0, 31, 59, 90, 120, 151, 181, 212, 243, 273, 304, 334 }

    TRACE( "time = %d", time )

    // Переводим количество секунд в количество дней с 1970 г.
    uiDate = time / 86400
    
    TRACE( "uiDate = %d", uiDate )

    // День недели, считая от нуля.
    DayOfWeek = ( uiDate + 3 ) % 7

    // Грубо оцениваем год.
    Year = uiDate / 365 + 1970

    // Убираем годы до начала отсчёта.
    y =  Year - 1970

    // Определяем количество дней в годах.
    days_to_year = y * 365 + ( y + 1 ) / 4 - ( y + 69 ) / 100 + ( y + 369 ) / 400

    //TRACE( "days_to_year = %d", days_to_year )
    
    // Делаем уточнение.
    if uiDate < days_to_year then
        
        Year = Year - 1
        y =  Year - 1970
        
        // Определяем количество дней в годах.
        days_to_year = y * 365 + ( y + 1 ) / 4 - ( y + 69 ) / 100 + ( y + 369 ) / 400
        
    end if

    // Определяем день года.
    uiDay = uiDate - days_to_year + 1

    // Грубо оценим месяц.
    Month = uiDay / 29 
    
    if Month > 11 then
    
        Month = 11
    
    end if
    
    // Определяем количество дней в истекших месяцах.
    days_to_month = DoM[ Month ]
    
    IsLeapYear = ( ( Year % 400 == 0 ) or ( Year % 4 == 0 ) ) and ( Year % 100 <> 0 )
    
    if Month > 2 and IsLeapYear then
    
        days_to_month = days_to_month + 1

    end if
    
    // Делаем уточнение.
    if uiDay <= days_to_month then
        
        Month = Month - 1

        days_to_month = DoM[ Month ]
               
        if Month > 2 and IsLeapYear then
        
            days_to_month = days_to_month + 1

        end if

    end if

    // Определяем день.
    Day = uiDay - days_to_month

    // Вычисляем эквивалент неполного дня в секундах.
    uiTime = time % 86400

    Seconds = uiTime % 60

    uiTime = uiTime / 60

    Minutes = uiTime % 60

    Hours = uiTime / 60
   
    // Сохраняем месяц.
    Month = Month + 1
    
    TRACE( "Year = %d", Year )
    TRACE( "Month = %d", Month )
    TRACE( "DayOfWeek = %d", DayOfWeek )
    TRACE( "Day = %d", Day )
    TRACE( "Hours = %d", Hours )
    TRACE( "Minutes = %d", Minutes )
    TRACE( "Seconds = %d", Seconds )

end sub

macro_command main()

    unsigned int time = 0

    // Считываем время из ПЛК в формате unix.
    GetData( time, "ПЛК", 4x, 10001, 1 )
    
    SWAPW( time, time )
    
    // Преобразуем элементы даты.
    GetLocalTime( time )

end macro_command
